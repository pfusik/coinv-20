TEMPO	equ	10
LINES	equ	80
BRUSHW	equ	3
BRUSHH	equ	2

scr	equ	$3200

src	equ	$80
dest	equ	$82
px	equ	$84
py	equ	$85
counter	equ	$90
counter_bcd	equ	$94

	org	$2800
main
	lda:tay	#0
	sta	dest
	ldx	>scr
cls_page
	stx	dest+1
	sta:rne	(dest),y+
	inx
	cpx	>scr+LINES*32
	bcc	cls_page

	jsr	w1
	mva	#$21	$22f
	mwa	#dl	$230
	ldx	#4
	mva:rpl	colors,x	$2c4,x-
	mwa	#trace	src
:3	jsr	draw

rainbow_loop
	lda	#11
	cmp:rne	^4b
	ldy	#LINES*2+4
	ldx	20
rainbow_line
	txa
	cmp	#$10
	scs:ora	#$10	; no grey
	sta	^4a
	sta	^16
	inx
	dey
	bne	rainbow_line
	jmp	rainbow_loop

draw
	mva	(src),0	px
	inw	src
	mva	(src),y	py
draw_loop
	inw	src
	lda	#TEMPO
	jsr	w8
	jsr	plot
	jsr	print_counter
	jsr	tomorrow
	lda	(src),0
	bmi	draw_ret
	and	#3
	sub	#1
	add:sta	px
	lda	(src),y
:2	lsr	@
	sub	#1
	add:sta	py
	jmp	draw_loop
draw_ret
	inw	src
	rts

plot
; yyy yyyyxxxx xxx
	mva	px	dest
	and	#7
	tax
	asl	dest
	lda	py
	lsr	@
	ror	dest
	lsr	@
	ror	dest
	lsr	@
	ror	dest
	lsr	@
	ror	dest
	and	#7
	add	>scr
	sta	dest+1
	ldy	#0
plot_line
	lda	masks,x
	ora:sta	(dest),y
	ift	BRUSHW==2
	cpx	#7
	bcc	plot_next_line
	iny
	lda	#$80
	ora:sta	(dest),y
	eli	BRUSHW==3
	cpx	#6
	bcc	plot_next_line
	iny
	lda	masks_right-6,x
	ora:sta	(dest),y
	eif
plot_next_line
	ift	BRUSHH
	tya
	and	#$f0
	add	#16
	tay
	cmp	#BRUSHH*16
	bcc	plot_line
	eif
	rts

print_counter
	mva	px	counter+1
	lda	#LINES
	sub	py
	lsr	@
	ror	counter+1
	lsr	@
	ror	counter+1
	sta	counter+2

	mva	#0	counter
	sta	counter_bcd
	sta	counter_bcd+1
	sta	counter_bcd+2
	sta	counter_bcd+3

	sed
	ldx	#24
counter_to_bcd
	asl	counter
	rol	counter+1
	rol	counter+2
	lda	counter_bcd
	adc:sta	counter_bcd
	lda	counter_bcd+1
	adc:sta	counter_bcd+1
	lda	counter_bcd+2
	adc:sta	counter_bcd+2
	lda	counter_bcd+3
	adc:sta	counter_bcd+3
	dex
	bne	counter_to_bcd
	cld

	ldx	#3
	ldy	#0
print_counter_out
	lda	counter_bcd,x
:4	lsr	@
	jsr	print_digit
	lda	counter_bcd,x
	and	#$f
	jsr	print_digit
	dex
	bpl	print_counter_out
	rts

print_digit
	bne	print_nonzero
	cmp	counter_str-1,y
	beq	print_space
	cpy	#0
	beq	print_space
print_nonzero
	ora	#'0'-$20
print_space
	sta	counter_str,y+
	rts

tomorrow
	lda	month_str
	cmp	#'1'+$a0
	lda	month_str+1
	scc:adc	#9
	tax
	lda	day_str
:4	asl	@
	add	day_str+1
	cmp	last_days-$d1,x
	bne	same_month
	lda	month_str+1
	cmp	#'9'+$a0
	bcc	month_1
	inc	month_str
	lda	#'0'+$a0-1
month_1
	add	#1
	sta	month_str+1
	lda	#'0'+$a0
	sta	day_str
	bne	tomorrow_1	; jmp
same_month
	lda	day_str+1
	cmp	#'9'+$a0
	bcc	tomorrow_1
	inc	day_str
	lda	#'0'+$a0-1
tomorrow_1
	add	#1
	sta	day_str+1
	rts

w1
	lda	#1
w8
	add	20
	cmp:rne	20
	rts

dl
	dta	$70,$70
	dta	$4b,a(scr)
:LINES-1	dta	$b
	dta	$70
	dta	$46,a(date)
	dta	$70
	dta	$42,a(scroll)
	dta	$41,a(dl)

date
	dta	d'date'*,':'+$a0,' '+$a0,'2'+$a0,'0'+$a0,'2'+$a0,'0'+$a0,'-'+$a0
month_str
	dta	'0'+$a0,'1'+$a0,'-'+$a0
day_str
	dta	'2'+$a0,'2'+$a0
	dta	d'   lost party   '*
	dta	d'  silly venture '*
scroll
	dta	d'Total Coronavirus Cases:'
counter_str
	dta	d'        '
	dta	d'                                           '
	dta	d'Vasco invites you to:   '
	dta	d'Lost Party      '
	dta	d'www.lostparty.pl/2020/     '
	dta	d'                                           '
	dta	d'Grey invites you to:    '
	dta	d'Silly Venture      '
	dta	d'sillyventure.eu     '

	dta	d'You have been watching   '
	dta	d'COINV-20   '
	dta	d'Idea and code by 0xF/Taquart      '
	dta	d'Music by X-Ray/Taquart     '

	dta	d'AtariArea 24h compo entry    '
	dta	d'http://www.atari.org.pl     '
	dta	d'                                           '
	ert	[*^dl]&$fc00

	ert	*>scr

last_days
	dta	l($d0+$31,$d0+$29,$d0+$31,$d0+$30)
	dta	l($d0+$30,$d0+$31,$d0+$30,$d0+$31)
	dta	l($d0+$31,$d0+$30,$d0+$31,$d0+$30)
	dta	l($d0+$31,$d0+$30,$d0+$31,$d0+$30)

masks
:8	dta	[[$ff>>BRUSHW]^$ff]>>#
	ift	BRUSHW==3
masks_right
	dta	$80,$c0
	eif

colors
	dta	$76,$0c,$00,$54,$00

trace
	icl	'trace.asx'

	run	main
