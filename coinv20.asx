TEMPO	equ	10
LINES	equ	80
BRUSHW	equ	3

scr	equ	$3200

src	equ	$80
dest	equ	$82
px	equ	$84
py	equ	$85

	org	$2800
main
	mwa	#trace	src
	jsr	w1
	mva	#$21	$22f
	mwa	#dl	$230
:3	jsr	draw

rainbow_loop
	lda	#15
	cmp:rne	^4b
	ldy	#LINES*2+4
	ldx	20
rainbow_line
	txa
	cmp	#$10
	scs:ora	#$10	; no grey
	sta	^4a
	sta	^16
	inx
	dey
	bne	rainbow_line
	jmp	rainbow_loop

draw
	mva	(src),0	px
	inw	src
	mva	(src),y	py
draw_loop
	inw	src
	lda	#TEMPO
	jsr	w8
	jsr	plot
	lda	(src),0
	bmi	draw_ret
	and	#3
	sub	#1
	add:sta	px
	lda	(src),y
:2	lsr	@
	sub	#1
	add:sta	py
	jmp	draw_loop
draw_ret
	inw	src
	rts

plot
; yyy yyyyxxxx xxx
	mva	px	dest
	and	#7
	tax
	asl	dest
	lda	py
	lsr	@
	ror	dest
	lsr	@
	ror	dest
	lsr	@
	ror	dest
	lsr	@
	ror	dest
	and	#7
	add	>scr
	sta	dest+1
	lda	masks,x
	ora:sta	(dest),y
	ift	BRUSHW==2
	cpx	#7
	bcc	plot_ret
	iny
	lda	#$80
	ora:sta	(dest),y
plot_ret
	eli	BRUSHW==3
	cpx	#6
	bcc	plot_ret
	iny
	lda	masks_right-6,x
	ora:sta	(dest),y
plot_ret
	eif
	rts

w1
	lda	#1
w8
	add	20
	cmp:rne	20
	rts

dl
:3	dta	$70
	dta	$4b,a(scr)
:LINES-1	dta	$b
	dta	$41,a(dl)
	ert	[*^dl]&$fc00

	ert	*>scr

masks
:8	dta	[[$ff>>BRUSHW]^$ff]>>#
	ift	BRUSHW==3
masks_right
	dta	$80,$c0
	eif

trace
	icl	'trace.asx'

	run	main
